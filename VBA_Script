Option Explicit

Sub Stock_information()

    Const FIRST_DATA_ROW As Integer = 2
    Const TICKER_COL As Integer = 1
    Const OPEN_COL As Integer = 3
    Const CLOSED_COL As Integer = 6
    Const INPUT_VOL_COL As Integer = 7
    Const COLOR_GREEN As Integer = 4
    Const COLOR_RED As Integer = 3
    
    
    'Declared and set as a worksheet
    Dim ws As Worksheet
    Dim ticker As String
    Dim ticker_volume As LongLong
    Dim Lastrow As Long
    Dim input_row As Long
    Dim open_price As Double
    Dim close_price As Double
    Dim price_change As Double
    Dim price_change_fraction As Double
    Dim output_row As Long
    Dim greatest_percent_increase_ticker As String
    Dim greatest_percent_increase As Double
    Dim greatest_percent_decrease_ticker As String
    Dim greatest_percent_decrease As Double
    Dim greatest_total_volume_ticker As String
    Dim greatest_total_volume As LongLong
    
    
    'Loop through all the stocks for up to one year
    For Each ws In Worksheets
        ws.Activate
        
        Lastrow = ws.Cells(1, TICKER_COL).End(xlDown).Row
        
        'Column Headings
        Range("I1").Value = "Ticker"
        Range("J1").Value = "Yearly Change"
        Range("K1").Value = "Percent Change"
        Range("L1").Value = "Total Stock Volume"
            
        Range("P1").Value = "Ticker"
        Range("Q1").Value = "Value"
        Range("O2").Value = "Greatest % Increase"
        Range("O3").Value = "Greatest % Decrease"
        Range("O4").Value = "Greatest Total Volume"
            
        'Set Up for first stock
        ticker_volume = 0
        open_price = Cells(FIRST_DATA_ROW, OPEN_COL)
        output_row = FIRST_DATA_ROW
        
        greatest_percent_increase = -999
        greatest_percent_decrease = 999
        greatest_total_volume = -999
        
        For input_row = FIRST_DATA_ROW To Lastrow
            ticker = ws.Cells(input_row, TICKER_COL).Value
            ticker_volume = ticker_volume + ws.Cells(input_row, INPUT_VOL_COL).Value
        
            'If last row of current stock
            If ws.Cells(input_row + 1, TICKER_COL).Value <> ticker Then
                
                'input
                close_price = ws.Cells(input_row, CLOSED_COL).Value
                
                'calculations
                price_change = close_price - open_price
                price_change_fraction = (price_change / open_price)
                If (price_change_fraction > greatest_percent_increase) Then
                    greatest_percent_increase = price_change_fraction
                    greatest_percent_increase_ticker = ticker
                End If
                If (price_change_fraction < greatest_percent_decrease) Then
                    greatest_percent_decrease = price_change_fraction
                    greatest_percent_decrease_ticker = ticker
                End If
                If (ticker_volume > greatest_total_volume) Then
                    greatest_total_volume = ticker_volume
                    greatest_total_volume_ticker = ticker
                End If
                
                'output
                Cells(output_row, 9).Value = ticker
                Cells(output_row, 10).Value = price_change
                If price_change > 0 Then
                    Cells(output_row, 10).Interior.ColorIndex = COLOR_GREEN
                Else
                    Cells(output_row, 10).Interior.ColorIndex = COLOR_RED
                End If
                Cells(output_row, 11).Value = FormatPercent(price_change_fraction)
                Cells(output_row, 12).Value = ticker_volume
                
                'prepare for next stock
                ticker_volume = 0
                open_price = Cells(input_row + 1, OPEN_COL)
                output_row = output_row + 1
                
            End If
            
        Next input_row
                'Output Greatest/Least
                Range("Q2").Value = FormatPercent(greatest_percent_increase)
                Range("P2").Value = greatest_percent_increase_ticker
                Range("Q3").Value = FormatPercent(greatest_percent_decrease)
                Range("P3").Value = greatest_percent_decrease_ticker
                Range("Q4").Value = greatest_total_volume
                Range("P4").Value = greatest_total_volume_ticker
            
    Next ws

End Sub
